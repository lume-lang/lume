namespace std::mem

import std (Pointer)
import std::ops (Dispose)

/// A heap-allocated value, containing a value of type `T`.
pub struct Box<T> {
    ptr: Pointer<T>;
}

impl<T> Box<T> {
    /// Allocates a new `Box<T>`, then places `value` into it.
    pub fn new(value: T) -> Box<T> {
        let size = std::type_of<T>().size;
        let ptr = std::mem::alloc<T>(size);

        ptr.write(value);

        Box<T> { ptr }
    }

    /// Gets the inner pointer, which the box is referring to.
    pub fn ptr(self) -> Pointer<T> {
        self.ptr
    }
}

use<T> Dispose in Box<T> {
    fn dispose(self) {
        std::mem::dealloc<T>(self.ptr);
    }
}

/// If the given value is a scalar type, it is boxed and it's
/// pointer is returned as a reference.
///
/// If the type is already a reference, it is returned as-is.
pub(internal) fn ref_or_box<T>(value: T) -> T {
    if std::type_of<T>().is_scalar() {
        let boxed: std::mem::Box<T> = std::mem::Box<T>::new(value);

        return boxed.ptr().as_ref();
    }

    value
}
